#Использовать configor

Перем МенеджерПараметров;
Перем КешЧтенияФайловПараметров;
Перем МенеджерПараметровНастроен;

Перем Лог;

Процедура УстановитьМенеджерПараметров(НовыйМенеджерПараметров) Экспорт
	
	МенеджерПараметров = НовыйМенеджерПараметров;

КонецПроцедуры

Функция ПолучитьМенеджерПараметров() Экспорт

	Возврат МенеджерПараметров;

КонецФункции

Процедура ПеречитатьПараметры() Экспорт

	Если МенеджерПараметровНастроен Тогда
		Лог.Отладка("Выполняю чтение файла параметров");
		МенеджерПараметров.Прочитать();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначениеПараметра(Знач ИмяПараметра, Знач ЗначениеПоУмолчанию = Неопределено) Экспорт

	Если Не МенеджерПараметровНастроен Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;

	Лог.Отладка("Получаю значение для <%1>", ИмяПараметра);
		
	Если НЕ МенеджерПараметров.ЧтениеВыполнено() Тогда
		ПеречитатьПараметры();
	КонецЕсли;

	Лог.Отладка("Получено значение <%1>", МенеджерПараметров.Параметр(ИмяПараметра, ЗначениеПоУмолчанию));

	Возврат МенеджерПараметров.Параметр(ИмяПараметра, ЗначениеПоУмолчанию);
	
КонецФункции

Процедура УстановитьФайлПараметров(Знач ПутьКФайлу) Экспорт
	
	МенеджерПараметров.УстановитьФайлПараметров(ПутьКФайлу);

	МенеджерПараметровНастроен = Истина;

КонецПроцедуры

Процедура НастроитьМенеджерПараметров(Знач ИмяФайлаПараметров, Знач КаталогПоиска = "", Знач РасширениеФайла = "") Экспорт
	
	МенеджерПараметров.УстановитьИмяФайла(ИмяФайлаПараметров);

	Если ЗначениеЗаполнено(РасширениеФайла) Тогда
		МенеджерПараметров.УстановитьРасширениеФайла(РасширениеФайла);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КаталогПоиска) Тогда
		МенеджерПараметров.ДобавитьКаталогПоиска(КаталогПоиска);
	КонецЕсли;

	МенеджерПараметровНастроен = Истина;

КонецПроцедуры

Функция Настроен() Экспорт
	Возврат МенеджерПараметровНастроен;
КонецФункции

МенеджерПараметровНастроен = Ложь;
МенеджерПараметров = Новый МенеджерПараметров;

Лог = Логирование.ПолучитьЛог("oscript.lib.cli_config");
//Лог.УстановитьУровень(УровниЛога.Отладка);