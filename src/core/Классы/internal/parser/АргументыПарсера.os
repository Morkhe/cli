#Использовать logos

Перем Аргумент Экспорт;
Перем Лог;

Функция ПриСозданииОбъекта(КлассОпции)
	Аргумент = КлассОпции;
КонецФункции

Функция Поиск(Знач ВходящиеАргументы, КонтекстПоиска) Экспорт

	Аргументы = Новый Массив;

	Для каждого Арг Из ВходящиеАргументы Цикл
		Аргументы.Добавить(Арг);
	КонецЦикла;
	
	Результат  = Новый Структура("РезультатПоиска, Аргументы", Аргумент.УстановленаИзПеременнойОкружения, Аргументы);

	Если Аргументы.Количество() = 0  Тогда
	
		Возврат Результат;

	КонецЕсли;

	Если (НЕ КонтекстПоиска.СбросОпций И 
		СтрНачинаетсяС(Аргументы[0], "-")
		И НЕ Аргументы[0] = "-")
		ИЛИ ПустаяСтрока(Аргументы[0])
		Тогда 
		Возврат Результат;
		
	КонецЕсли;

	АргументКонтекст = КонтекстПоиска.Аргументы[Аргумент];
	Если АргументКонтекст = Неопределено Тогда
		АргументКонтекст = Новый Массив;
	КонецЕсли;
	АргументКонтекст.Добавить(Аргументы[0]);
	КонтекстПоиска.Аргументы.Вставить(Аргумент,АргументКонтекст);
	
	Результат.РезультатПоиска = Истина;
	Аргументы.Удалить(0);
	
	Результат.Аргументы  = Аргументы;
	Возврат Результат;

КонецФункции

Функция Приоритет() Экспорт
	Возврат 8;
КонецФункции

Функция ВСтроку() Экспорт
	Возврат Аргумент.Имя;
КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.cli_class_arg");
